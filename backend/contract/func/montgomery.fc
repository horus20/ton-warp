const int BASE = 0x10000000000000000;
;; Q in hex - 0x30644e72e131a029b85045b68181585d2833e84879b9709143e1f593f0000001
;; Q - 21888242871839275222246405745257275088548364400416034343698204186575808495617
const int Q = 21888242871839275222246405745257275088548364400416034343698204186575808495617;
const int R = 944936681149208446651664254269745548490766851729442924617792859073125903783;  ;; rSquare
const int M = 14042775128853446655;

const int Q0 = 4891460686036598785;
const int Q1 = 2896914383306846353;
const int Q2 = 13281191951274694749;
const int Q3 = 3486998266802970665;

tuple getR() inline {
  return unsafe_tuple([1997599621687373223, 6052339484930628067, 10108755138030829701, 150537098327114917]);
}

_ madd0(int a, int b, int c) inline {
  var (hi, lo) = (a * b) /% BASE;
  var carry = (lo + c) / BASE;

  return (hi + carry);
}

_ madd1(int a, int b, int c) inline {
  var (hi, lo) = (a * b) /% BASE;
  var (carry, lo) = (lo + c) /% BASE;

  return [(hi + carry), lo];
}

;; hi, lo = a*b + c + d
_ madd2(int a, int b, int c, int d) inline {
  var (hi, lo) = (a * b) /% BASE;
  var (carry, c) = (c + d) /% BASE;
  hi = hi + carry;
  var (carry, lo) = (lo + c) /% BASE;

  return [(hi + carry), lo];
}

_ madd3(int a, int b, int c, int d, int e) inline {
  var (hi, lo) = (a * b) /% BASE;
  var (carry, c) = (c + d) /% BASE;
  hi = hi + carry;
  var (carry, lo) = (lo + c) /% BASE;

  return [(hi + e + carry), lo];
}

tuple checkOverflow(int z0, int z1, int z2, int z3) inline {
  var z = unsafe_tuple([z0, z1, z2, z3]);
  var zI = longToInt(z);
  if (zI > Q) {
		z = long(zI - Q);
  }

  return z;
}

tuple mulGeneric(tuple x, tuple y) inline {
  int t0 = 0;
  int t1 = 0;
  int t2 = 0;
  int t3 = 0;
  int c0 = 0;
  int c1 = 0;
  int c2 = 0;
  int z0 = 0;
  int z1 = 0;
  int z2 = 0;
  int z3 = 0;

  ;; round 0
  var v = 0;
  if (x.length() > 0) {
    v = x.getByIndex(0);
  }
  var tmp = v * y.getByIndex(0);
  c1 = tmp / BASE;
  c0 = tmp % BASE;
  var m = c0 * M % BASE;
  c2 = madd0(m, Q0, c0);
  [c1, c0] = madd1(v, y.getByIndex(1), c1);
  [c2, t0] = madd2(m, Q1, c2, c0);
  [c1, c0] = madd1(v, y.getByIndex(2), c1);
  [c2, t1] = madd2(m, Q2, c2, c0);
  [c1, c0] = madd1(v, y.getByIndex(3), c1);
  [t3, t2] = madd3(m, Q3, c0, c2, c1);

  ;; round 1
  v = 0;
  if (x.length() > 1) {
    v = x.getByIndex(1);
  }
  [c1, c0] = madd1(v, y.getByIndex(0), t0);
  m = c0 * M % BASE;
  c2 = madd0(m, Q0, c0);
  [c1, c0] = madd2(v, y.getByIndex(1), c1, t1);
  [c2, t0] = madd2(m, Q1, c2, c0);
  [c1, c0] = madd2(v, y.getByIndex(2), c1, t2);
	[c2, t1] = madd2(m, Q2, c2, c0);
	[c1, c0] = madd2(v, y.getByIndex(3), c1, t3);
	[t3, t2] = madd3(m, Q3, c0, c2, c1);

  ;; round 2
  v = 0;
  if (x.length() > 2) {
    v = x.getByIndex(2);
  }
  [c1, c0] = madd1(v, y.getByIndex(0), t0);
  m = c0 * M % BASE;
  c2 = madd0(m, Q0, c0);
  [c1, c0] = madd2(v, y.getByIndex(1), c1, t1);
  [c2, t0] = madd2(m, Q1, c2, c0);
  [c1, c0] = madd2(v, y.getByIndex(2), c1, t2);
	[c2, t1] = madd2(m, Q2, c2, c0);
	[c1, c0] = madd2(v, y.getByIndex(3), c1, t3);
	[t3, t2] = madd3(m, Q3, c0, c2, c1);

  ;; round 3
  v = 0;
  if (x.length() > 3) {
    v = x.getByIndex(3);
  }
  [c1, c0] = madd1(v, y.getByIndex(0), t0);
  m = c0 * M % BASE;
  c2 = madd0(m, Q0, c0);
  [c1, c0] = madd2(v, y.getByIndex(1), c1, t1);
  [c2, z0] = madd2(m, Q1, c2, c0);
  [c1, c0] = madd2(v, y.getByIndex(2), c1, t2);
  [c2, z1] = madd2(m, Q2, c2, c0);
	[c1, c0] = madd2(v, y.getByIndex(3), c1, t3);
	[z3, z2] = madd3(m, Q3, c0, c2, c1);

  ;;return unsafe_tuple([z, t0, t1, t2, t3, c0, c1, c2, m, v]);
  return checkOverflow(z0, z1, z2, z3);
}

tuple addGeneric(tuple x, tuple y) inline {
  int z0 = 0;
  int z1 = 0;
  int z2 = 0;
  int z3 = 0;
  int carry = 0;

  int tmp = x.getByIndex(0) + y.getByIndex(0);
  carry = tmp / BASE;
  z0 = tmp % BASE;

  tmp = x.getByIndex(1) + y.getByIndex(1) + carry;
  carry = tmp / BASE;
  z1 = tmp % BASE;

  tmp = x.getByIndex(2) + y.getByIndex(2) + carry;
  carry = tmp / BASE;
  z2 = tmp % BASE;

  tmp = x.getByIndex(3) + y.getByIndex(3) + carry;
  carry = tmp / BASE;
  z3 = tmp % BASE;

  return checkOverflow(z0, z1, z2, z3);
}

;; the following lines implement z = z * 1
;; with a modified CIOS montgomery multiplication
tuple fromMont(int z0, int z1, int z2, int z3) inline {
  repeat (4) {
    var m = z0 * M % BASE;
    var c = madd0(m, Q0, z0);
    [c, z0] = madd2(m, Q1, z1, c);
    [c, z1] = madd2(m, Q2, z2, c);
    [c, z2] = madd2(m, Q3, z3, c);
    z3 = c;
  }

  return checkOverflow(z0, z1, z2, z3);
}
